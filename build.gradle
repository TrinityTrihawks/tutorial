plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22' apply false
}

allprojects {
    group = 'org.first.tutorial'
    version = '0.1.0'
}

subprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

configure(subprojects.findAll { it.path.startsWith(':java:') }) {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencies {
        testImplementation('org.junit.jupiter:junit-jupiter:5.10.2')
    }
}

configure(subprojects.findAll { it.path.startsWith(':kotlin:') }) {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    kotlin {
        jvmToolchain(17)
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        testImplementation('org.junit.jupiter:junit-jupiter:5.10.2')
    }
}
